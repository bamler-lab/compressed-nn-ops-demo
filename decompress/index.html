<!DOCTYPE html>
<html>

<head>
  <meta charset="utf-8" />
  <script src="main.js"></script>
  <script id="vertex-shader" type="x-shader/x-vertex">#version 300 es
    precision highp float;

    in vec4 vertex_position;
    out vec2 float_pos;

    void main() {
      float_pos = vertex_position.xy;
      gl_Position = vertex_position;
    }
  </script>
  <script id="create-lookup-shader" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    precision mediump int;
    uniform mediump usampler2D compressedMatrix;
    uniform int width;
    uniform int height;

    in vec2 float_pos;
    out lowp uint val;

    void main() {
      uvec2 pos = uvec2(
        uint(0.5 * float(width) * (float_pos.x + 1.0)),
        uint(0.5 * float(height) * (float_pos.y + 1.0))
      );

      int y = 2;
      
      uint threshold = pos.x;
      for (int x=2; x!=200; x+=2) { // 200 is a somewhat arbitrary cutoff here.
        uint cdf = texelFetch(compressedMatrix, ivec2(x, y), 0).r;
        if (cdf > threshold) {
          val = uint(x - 2);
          return;
        }
      }
    }
  </script>
  <script id="compressed-mat-vec-mul-shader" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    precision mediump int;
    uniform mediump usampler2D compressedMatrix;
    uniform lowp usampler2D lookupTable;
    uniform mediump isampler2D vector;
    uniform int width;
    uniform int height;

    in vec2 float_pos;
    out mediump int val;

    void main() {
      uint column = uint(0.5 * float(width) * (float_pos.x + 1.0));
      ivec2 offset_pos = ivec2(int(2u * (column & 511u)), int(column >> 9));
      uint offset_lo = texelFetch(compressedMatrix, offset_pos, 0).r;
      uint offset_hi = texelFetch(compressedMatrix, ivec2(offset_pos.x + 1, offset_pos.y), 0).r;
      uint read_index = offset_lo | (offset_hi << 16);

      // Initialize decoder
      ivec2 read_pos = ivec2(read_index & 1023u, read_index >> 10);
      uint head = texelFetch(compressedMatrix, read_pos, 0).r;
      ++read_index;
      read_pos = ivec2(read_index & 1023u, read_index >> 10);
      head = (head << 16) | texelFetch(compressedMatrix, read_pos, 0).r;
      val = 0;
      ivec2 model_lookup = ivec2(0, 2);

      for (int i=0; i!=1024; ++i) { // TODO: 1024 is not guaranteed to be representable in mediump int
        uint quantile = head & uint(0x0fff);
        model_lookup.x = int(texelFetch(lookupTable, ivec2(quantile, 0), 0).r);
        uint left_cdf = texelFetch(compressedMatrix, model_lookup, 0).r;
        ++model_lookup.x;
        uint matrix_entry_uint = texelFetch(compressedMatrix, model_lookup, 0).r;
        int matrix_entry = int(matrix_entry_uint) - 0x8000;
        ++model_lookup.x;
        uint right_cdf = texelFetch(compressedMatrix, model_lookup, 0).r;
        uint prob =  right_cdf - left_cdf;
        uint remainder = quantile - left_cdf;
        head = (head >> 12) * prob + remainder;
        int vector_entry = texelFetch(vector, ivec2(i, 0), 0).r;
        val += matrix_entry * vector_entry;
        if ((head >> 16) == 0u) {
          ++read_index;
          read_pos = ivec2(read_index & 1023u, read_index >> 10);
          head = (head << 16) | texelFetch(compressedMatrix, read_pos, 0).r;
        }
      }
      val = val % int(1024); // Temporary hack to prevent values from exploding
    }
  </script>
  <script id="partpartsum-shader" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    precision mediump int;
    uniform lowp usampler2D input_data;
    uniform int width;
    uniform int height;

    in vec2 float_pos;
    out mediump uint sum;

    void main() {
      ivec2 pos = ivec2(
        int(0.5 * float(width) * (float_pos.x + 1.0)),
        int(0.5 * float(height) * (float_pos.y + 1.0))
      );

      sum = 0u;
      for (int i=pos.x & (1023 - 7); i!=pos.x; ++i) {
        sum = (sum + texelFetch(input_data, ivec2(i, pos.y), 0).r) % 10000u;
      }
      sum = (sum + texelFetch(input_data, pos, 0).r) % 10000u;
    }
  </script>
  <script id="partsum-shader" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    precision mediump int;
    uniform lowp usampler2D input_data;
    uniform int width;
    uniform int height;

    in vec2 float_pos;
    out mediump uint sum;

    void main() {
      ivec2 pos = ivec2(
        int(0.5 * float(width) * (float_pos.x + 1.0)),
        int(0.5 * float(height) * (float_pos.y + 1.0))
      );

      sum = 0u;
      int endpos = (pos.x << 3) | 7;
      int startpos = endpos & (((127 - 3) * 8) | 7);
      for (int i=startpos; i!=endpos; i+=8) {
        sum = (sum + texelFetch(input_data, ivec2(i, pos.y), 0).r) % 10000u;
      }
      sum = (sum + texelFetch(input_data, ivec2(endpos, pos.y), 0).r) % 10000u;
    }
  </script>
  <script id="rowsum-shader" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    precision mediump int;
    uniform lowp usampler2D input_data;
    uniform int width;
    uniform int height;

    in vec2 float_pos;
    out mediump uint sum;

    void main() {
      ivec2 pos = ivec2(
        int(0.5 * float(width) * (float_pos.x + 1.0)),
        int(0.5 * float(height) * (float_pos.y + 1.0))
      );

      sum = 0u;
      int endpos = pos.x * 4 + 3;
      for (int i=3; i!=endpos; i+=4) {
        sum = (sum + texelFetch(input_data, ivec2(i, pos.y), 0).r) % 10000u;
      }
      sum = (sum + texelFetch(input_data, ivec2(endpos, pos.y), 0).r) % 10000u;
    }
  </script>
  <script id="sumsumpart-shader" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    precision mediump int;
    uniform lowp usampler2D input_data;
    uniform int width;
    uniform int height;
    // uniform int input_col;

    in vec2 float_pos;
    out mediump uint sum;

    void main() {
      int input_col = 31;
      ivec2 pos = ivec2(
        int(0.5 * float(width) * (float_pos.x + 1.0)),
        int(0.5 * float(height) * (float_pos.y + 1.0))
      );

      sum = 0u;
      int startpos = pos.y & (1023 - 31);
      for (int i=startpos; i!=pos.y; ++i) {
        sum = (sum + texelFetch(input_data, ivec2(input_col, i), 0).r) % 10000u;
      }
      sum = (sum + texelFetch(input_data, ivec2(input_col, pos.y), 0).r) % 10000u;
    }
  </script>
  <script id="sumsum-shader" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    precision mediump int;
    uniform lowp usampler2D input_data;
    uniform int width;
    uniform int height;
    // uniform int input_col;

    in vec2 float_pos;
    out mediump uint sum;

    void main() {
      ivec2 pos = ivec2(
        int(0.5 * float(width) * (float_pos.x + 1.0)),
        int(0.5 * float(height) * (float_pos.y + 1.0))
      );

      sum = 0u;
      int endpos = (pos.y * 32) | 31;
      for (int i=31; i!=endpos; i+=32) {
        sum = (sum + texelFetch(input_data, ivec2(0, i), 0).r) % 10000u;
      }
      sum = (sum + texelFetch(input_data, ivec2(0, endpos), 0).r) % 10000u;
    }
  </script>
  <script>
    window.addEventListener('DOMContentLoaded', run);
  </script>
</head>

<body>
  <canvas id="canvas" width="512" height="256"></canvas>
</body>

</html>
