<!DOCTYPE html>
<html>

<head>
  <meta charset="utf-8" />
  <script src="main.js"></script>
  <script id="vertex-shader" type="x-shader/x-vertex">#version 300 es
    precision highp float;

    in vec4 vertex_position;
    out vec2 float_pos;

    void main() {
      float_pos = vertex_position.xy;
      gl_Position = vertex_position;
    }
  </script>
  <script id="create-lookup-shader" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    precision mediump int;
    uniform mediump usampler2D compressedData;
    uniform int width;
    uniform int height;

    in vec2 float_pos;
    out lowp uint val;

    void main() {
      uvec2 pos = uvec2(
        uint(0.5 * float(width) * (float_pos.x + 1.0)),
        uint(0.5 * float(height) * (float_pos.y + 1.0))
      );

      uint threshold = pos.x;
      for (int x=1; x!=200; ++x) { // 200 is a somewhat arbitrary cutoff here.
        uint cdf = texelFetch(compressedData, ivec2(x, 0), 0).r;
        if (cdf > threshold) {
          val = uint(x - 1);
          return;
        }
      }
    }
  </script>
  <script id="decode-shader" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    precision mediump int;
    uniform mediump usampler2D coderHeads;
    uniform mediump usampler2D compressedData;
    uniform mediump usampler2D lookupTable;
    uniform lowp usampler2D coderOffsets1;
    uniform lowp usampler2D coderOffsets2;
    uniform lowp usampler2D coderOffsets3;
    uniform lowp usampler2D coderOffsets4;
    uniform mediump usampler2D coderOffsets5;
    // uniform uint matrixBegin;
    uniform int width;
    uniform int height;

    in vec2 float_pos;
    layout(location=0) out mediump uint val;
    layout(location=1) out mediump uvec2 newCoderHead;

    void main() {
      ivec2 pos = ivec2(
        int(0.5 * float(width) * (float_pos.x + 1.0)),
        int(0.5 * float(height) * (float_pos.y + 1.0))
      );

      uvec2 head = texelFetch(coderHeads, pos, 0).xy;

      // Refill `head` if necessary:
      if (head.x == 0u) {
        head.x = head.y;
        uint readOffset = //matrixBegin - 1u +
          texelFetch(coderOffsets1, pos, 0).r +
          texelFetch(coderOffsets2, ivec2(pos.x / 4, pos.y), 0).r +
          texelFetch(coderOffsets3, ivec2(pos.x / 32, pos.y), 0).r +
          texelFetch(coderOffsets4, ivec2(0, pos.y), 0).r +
          texelFetch(coderOffsets5, ivec2(0, pos.y / 32), 0).r;
        ivec2 readPos = ivec2(int(readOffset & 1023u), int(readOffset >> 10));
        head.y = texelFetch(compressedData, readPos, 0).r;
      }

      // Decode value:
      uint headVal = (head.x << 16) | head.y;
      uint quantile = headVal & uint(0x0fff);
      lowp uint symbol_id = texelFetch(lookupTable, ivec2(int(quantile), 0), 0).r;
      val = symbol_id;
      mediump uint left_cdf = texelFetch(compressedData, ivec2(int(symbol_id), 0), 0).r;
      mediump uint right_cdf = texelFetch(compressedData, ivec2(int(symbol_id) + 1, 0), 0).r;
      highp uint prob = right_cdf - left_cdf;
      highp uint remainder = quantile - left_cdf;
      highp uint newHead = (headVal >> 12) * prob + remainder;
      newCoderHead = uvec2(newHead >> 16, newHead & uint(0xffff));
    }
  </script>
  <script id="sumOffsets0-shader" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    precision mediump int;
    uniform mediump usampler2D coderHeads;
    uniform int width;
    uniform int height;

    in vec2 float_pos;
    out lowp uint sum;

    void main() {
      ivec2 pos = ivec2(
        int(0.5 * float(width) * (float_pos.x + 1.0)),
        int(0.5 * float(height) * (float_pos.y + 1.0))
      );

      sum = 0u;
      // TODO: manually unroll this loop and measure performance difference.
      // Also, maybe do comparison to zero in "decode" shader write result to separate texture.
      for (int i=pos.x & (1023 - 3); i!=pos.x; ++i) {
        sum += uint(texelFetch(coderHeads, ivec2(i, pos.y), 0).x == 0u);
      }
      sum += uint(texelFetch(coderHeads, pos, 0).x == 0u);
    }
  </script>
  <script id="sumOffsets1-shader" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    precision mediump int;
    uniform lowp usampler2D sumOffsets1;
    uniform int width;
    uniform int height;

    in vec2 float_pos;
    out lowp uint sum;

    void main() {
      ivec2 pos = ivec2(
        int(0.5 * float(width) * (float_pos.x + 1.0)),
        int(0.5 * float(height) * (float_pos.y + 1.0))
      );

      sum = 0u;
      int endpos = (pos.x << 2) | 3;
      int startpos = endpos & (((255 - 7) * 4) | 3);
      for (int i=startpos; i!=endpos; i+=4) {
        sum += texelFetch(sumOffsets1, ivec2(i, pos.y), 0).r;
      }
      sum += texelFetch(sumOffsets1, ivec2(endpos, pos.y), 0).r;
    }
  </script>
  <script id="sumOffsets2-shader" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    precision mediump int;
    uniform lowp usampler2D sumOffsets2;
    uniform int width;
    uniform int height;

    in vec2 float_pos;
    out mediump uint sum;

    void main() {
      ivec2 pos = ivec2(
        int(0.5 * float(width) * (float_pos.x + 1.0)),
        int(0.5 * float(height) * (float_pos.y + 1.0))
      );

      sum = 0u;
      int endpos = (pos.x << 3) | 7;
      for (int i=7; i!=endpos; i+=8) {
        sum += texelFetch(sumOffsets2, ivec2(i, pos.y), 0).r;
      }
      sum += texelFetch(sumOffsets2, ivec2(endpos, pos.y), 0).r;
    }
  </script>
  <script id="sumOffsets3-shader" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    precision mediump int;
    uniform mediump usampler2D sumOffsets3;
    uniform int width;
    uniform int height;

    in vec2 float_pos;
    out mediump uint sum;

    void main() {
      int input_col = 31;
      ivec2 pos = ivec2(
        int(0.5 * float(width) * (float_pos.x + 1.0)),
        int(0.5 * float(height) * (float_pos.y + 1.0))
      );

      sum = 0u;
      int startpos = pos.y & (1023 - 31);
      for (int i=startpos; i!=pos.y; ++i) {
        sum += texelFetch(sumOffsets3, ivec2(input_col, i), 0).r;
      }
      sum += texelFetch(sumOffsets3, ivec2(input_col, pos.y), 0).r;
    }
  </script>
  <script id="sumOffsets4-shader" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    precision mediump int;
    uniform mediump usampler2D sumOffsets4;
    uniform int width;
    uniform int height;

    in vec2 float_pos;
    out highp uint sum;

    void main() {
      ivec2 pos = ivec2(
        int(0.5 * float(width) * (float_pos.x + 1.0)),
        int(0.5 * float(height) * (float_pos.y + 1.0))
      );

      sum = 0u;
      int endpos = (pos.y * 32) | 31;
      for (int i=31; i!=endpos; i+=32) {
        sum += texelFetch(sumOffsets4, ivec2(0, i), 0).r;
      }
      sum += texelFetch(sumOffsets4, ivec2(0, endpos), 0).r;
    }
  </script>
  <script>
    window.addEventListener('DOMContentLoaded', run);
  </script>
</head>

<body>
  <canvas id="canvas" width="512" height="256"></canvas>
</body>

</html>
