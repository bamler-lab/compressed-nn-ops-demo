<!DOCTYPE html>
<html lang="en">

<head>
  <meta charset="utf-8">
  <title>Entropy decoding in WebGL</title>
  <script src="main.js"></script>
  <script id="vertex-shader" type="x-shader/x-vertex">#version 300 es
    precision highp float;

    in vec4 vertex_position;
    out vec2 float_pos;

    void main() {
      float_pos = vertex_position.xy;
      gl_Position = vertex_position;
    }
  </script>
  <script id="createPpf-shader" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    precision highp int;

    // Default input uniforms:
    uniform mediump int width;
    uniform mediump int height;
  
    // Input textures:
    uniform mediump usampler2D compressedData;

    // Custom input uniforms:
    uniform highp int cursor;

    in highp vec2 float_pos; // NOTE: for 10-bit positions, `mediump vec2` should work as well.

    // Output textures:
    layout(location=0) out lowp uint ppf;

    void main() {
      lowp uint quantile = uint(128.0 * (float_pos.x + 1.0)); // Goes from 0 to 255 inclusively.

      highp uint myCursor = uint(cursor) + 2u;

      highp ivec2 readPos = ivec2(int(myCursor & 1023u), int(myCursor >> 10));
      highp uint numRows = texelFetch(compressedData, readPos, 0).r;      
      myCursor += 1u;
      readPos = ivec2(int(myCursor & 1023u), int(myCursor >> 10));
      numRows |= texelFetch(compressedData, readPos, 0).r << 16;
      myCursor += 1u;
      
      myCursor += 2u + numRows * 2u; // Skip `gridSpacing` and `cursors`.

      readPos = ivec2(int(myCursor & 1023u), int(myCursor >> 10));
      lowp uint word = texelFetch(compressedData, readPos, 0).r;
      lowp uint gridStartUnsigned = word & 0xffu;
      lowp uint gridSize = word >> 8;
      myCursor += 1u;

      lowp uint pointerPast = 0u;
      if (quantile == 255u) {
        // By specification of the file format, the only entry of `cdf` that is allowed to be
        // *semantically* larger than 255 is the last entry, and the last entry indeed has
        // *semantically* have the value 256, which is indeed larger than 255. However, this
        // value is stored as 0 in the file format (due to wrapping), and so it wouldn't work
        // in our loop below.
        pointerPast = gridSize;
      } else {
        while (pointerPast != 254u) {
          readPos = ivec2(int(myCursor & 1023u), int(myCursor >> 10));
          word = texelFetch(compressedData, readPos, 0).r;
          myCursor += 1u;

          lowp uint cdf = word & 0xffu;
          if (cdf > quantile) {
            break;
          }
          pointerPast += 1u;

          cdf = word >> 8;
          if (cdf > quantile) {
            break;
          }
          pointerPast += 1u;
        }
      }

      ppf = pointerPast - 1u;
}
  </script>
  <script id="matVecMul-shader" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    precision highp int;

    // Default input uniforms:
    uniform mediump int width;
    uniform mediump int height;
  
    // Input textures:
    uniform mediump usampler2D compressedData;
    uniform lowp usampler2D ppf;
    uniform lowp isampler2D inputVec;

    // Custom input uniforms:
    uniform highp int cursor;

    in highp vec2 float_pos; // NOTE: for 10-bit positions, `mediump vec2` should work as well.

    // Output textures:
    layout(location=0) out lowp int outputVec;

    void main() {
      highp uint myCursor = uint(cursor);

      highp ivec2 readPos = ivec2(int(myCursor & 1023u), int(myCursor >> 10));
      highp uint numCols = texelFetch(compressedData, readPos, 0).r;      
      myCursor += 1u;
      readPos = ivec2(int(myCursor & 1023u), int(myCursor >> 10));
      numCols |= texelFetch(compressedData, readPos, 0).r << 16;
      myCursor += 1u;

      readPos = ivec2(int(myCursor & 1023u), int(myCursor >> 10));
      highp uint numRows = texelFetch(compressedData, readPos, 0).r;      
      myCursor += 1u;
      readPos = ivec2(int(myCursor & 1023u), int(myCursor >> 10));
      numRows |= texelFetch(compressedData, readPos, 0).r << 16;
      myCursor += 1u;

      readPos = ivec2(int(myCursor & 1023u), int(myCursor >> 10));
      highp uint gridSpacingUint = texelFetch(compressedData, readPos, 0).r;      
      myCursor += 1u;
      readPos = ivec2(int(myCursor & 1023u), int(myCursor >> 10));
      gridSpacingUint |= texelFetch(compressedData, readPos, 0).r << 16;
      myCursor += 1u;
      highp float gridSpacing = uintBitsToFloat(gridSpacingUint);

      highp uint row = uint(0.5 * float(numRows) * (float_pos.x + 1.0));

      highp uint gridCursor = myCursor + numRows * 2u; // Skip `gridSpacing` and `cursors`.
      readPos = ivec2(int(gridCursor & 1023u), int(gridCursor >> 10));
      lowp uint word = texelFetch(compressedData, readPos, 0).r;
      lowp int gridStartMinus128 = int(((word & 0xffu) + 128u) & 0xffu) - 256;
      lowp uint gridSize = word >> 8;
      highp uint cdfStart = gridCursor + 1u;
      highp uint payloadStart = cdfStart + (gridSize + 2u) / 2u;

      myCursor += 2u * row; // `myCursor` now points to `cursors[row]`.
      readPos = ivec2(int(myCursor & 1023u), int(myCursor >> 10));
      highp uint offset = texelFetch(compressedData, readPos, 0).r;      
      myCursor += 1u;
      readPos = ivec2(int(myCursor & 1023u), int(myCursor >> 10));
      offset |= texelFetch(compressedData, readPos, 0).r << 16;
      
      myCursor = payloadStart + offset; // `myCursor` now points to the correct position within the compressed data.

      readPos = ivec2(int(myCursor & 1023u), int(myCursor >> 10));
      highp uint state = texelFetch(compressedData, readPos, 0).r << 16;
      myCursor += 1u;
      readPos = ivec2(int(myCursor & 1023u), int(myCursor >> 10));
      state |= texelFetch(compressedData, readPos, 0).r;
      myCursor += 1u;

      highp int accumulator = 0;

      for (highp uint col=0u; col!=numCols; col+=1u) {
        lowp uint quantile = state & 0xffu;
        lowp uint symbolId = texelFetch(ppf, ivec2(int(quantile), 0), 0).r;

        highp uint cdfCursor = cdfStart + symbolId / 2u;
        readPos = ivec2(int(cdfCursor & 1023u), int(cdfCursor >> 10));
        word = texelFetch(compressedData, readPos, 0).r;
        mediump uint leftCdfShift = (symbolId & 0x01u) << 3;
        mediump uint leftCdf = (word >> leftCdfShift) & 0xffu;

        cdfCursor = cdfStart + (symbolId + 1u) / 2u;
        readPos = ivec2(int(cdfCursor & 1023u), int(cdfCursor >> 10));
        word = texelFetch(compressedData, readPos, 0).r;
        mediump uint rightCdf = (word >> (8u ^ leftCdfShift)) & 0xffu;
        
        highp uint probability = ((rightCdf | 0x0100u) - leftCdf) & 0xffu;
        highp uint remainder = quantile - leftCdf;
        state = (state >> 8) * probability + remainder;

        if ((state >> 16) == 0u) {
          readPos = ivec2(int(myCursor & 1023u), int(myCursor >> 10));
          state = (state << 16) | texelFetch(compressedData, readPos, 0).r;
          myCursor += 1u;
        }

        // Reinterpret unsigned as signed integers.
        mediump uint symbolId_u16 = uint(symbolId);
        mediump int symbol = int((symbolId_u16 + 128u) & 0xffu) + gridStartMinus128;

        accumulator += symbol * texelFetch(inputVec, ivec2(int(col), 0), 0).r;
      }

      outputVec = int(round(float(accumulator) * gridSpacing));
    }
  </script>
  <script>
    window.addEventListener('DOMContentLoaded', run);
  </script>
</head>

<body>
  <h1>Compressed Matrix-Vector Multiplication in WebGL</h1>
  <h2>How to run</h2>
  <p>
    Due to cross-site scripting protection, you can't open this website directly from a local file system.
    Instead, open a terminal, <code>cd</code> into the directory that contains this HTML file, and run
    <code>python -m http.server</code>.
    Then, open the URL that is printed to the terminal in a web browser (usually <a
      href="http://0.0.0.0:8000/">http://0.0.0.0:8000/</a>).
  </p>
  <h2>Output</h2>
  <p id="output">
    please wait ...
  </p>
  <canvas id="canvas" width="512" height="256"></canvas>
</body>

</html>
